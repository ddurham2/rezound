dnl vim:tw=78
dnl Process this file with autoconf to build a configure script
dnl
dnl ReZound configure.in Written by Anthony Ventimiglia
dnl
dnl Copyright (C) 2002 - Anthony Ventimiglia
dnl 
dnl This file is part of ReZound, an audio editing application, 
dnl 
dnl ReZound is free software; you can redistribute it and/or modify it under the
dnl terms of the GNU General Public License as published by the Free Software
dnl Foundation; either version 2 of the License, or (at your option) any later
dnl version.
dnl 
dnl ReZound is distributed in the hope that it will be useful, but WITHOUT ANY
dnl WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
dnl FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
dnl details.
dnl 
dnl You should have received a copy of the GNU General Public License along with
dnl this program; if not, write to the Free Software Foundation, Inc., 59 Temple
dnl Place - Suite 330, Boston, MA  02111-1307, USA


dnl Process this file with autoconf to produce a configure script.
AC_INIT(config/common.h)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config/config.h)
AM_INIT_AUTOMAKE(ReZound, 0.1prealpha)
AC_DISABLE_SHARED   dnl This only builds static libs 

dnl --------------------|
dnl Checks for programs.|
dnl --------------------|
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
dnl AC_PROG_CXX automatically sets CXXFLAGS to -g -O2 if g++ is c++ compiler
dnl and CXXFLAGS hasn't been set, so by setting CXXFLAGS, we can avoid this.
CXXFLAGS=
AC_PROG_CXX
dnl These are some custom checks to see if cc accepts some extra warning flags
dnl see config/cxx.m4 for macro definition
ajv_CXX_FLAG(-Wno-unused-function)
ajv_CXX_FLAG(-Wno-unused-variable)
AM_PROG_LIBTOOL
AC_PROG_CPP

dnl ---------------------|
dnl Checks for libraries.|
dnl ---------------------|
AC_CHECK_LIB(m, sqrt)
AC_CHECK_LIB(dl, dlopen)
AC_CHECK_LIB(pthread, pthread_create)

dnl see config/cxx-lib.m4 for ajv_ macro definitions. 
dnl
dnl I found that afSetVirtualSampleFormat() was missing from earlier versions
dnl of audifile, so this is a little better than checking for a random
dnl function in libaudiofile. I still have to work this so configure will
dnl abort if the test fails.
ajv_CHECK_LIB_ABORT(audiofile, afSetVirtualSampleFormat,
AUDIOFILE,[http://oss.sgi.com/projects/audiofile/])

dnl I added an extra argument to ajv_CXX_CHECK_LIB which allows us to pass
dnl extra -l flags so this test (as suggested by Davy) would work. 
dnl As for potential problems with a compiler not recognizing namespaces:
dnl For now let's worry about g++, and sooner or later everything should
dnl become ANSI and recognize namespaces.
ajv_CXX_CHECK_LIB([ccgnu],ost::Mutex,cc++/thread.h,[http://www.gnu.org/directory/commoncpp.html],[-lpthread -ldl])

dnl This is a check for a class in a C++ library. To pinpoint
dnl the specific version we need we could change what class is linked.
ajv_CXX_CHECK_LIB([FOX],FXApp,fox/fx.h,[http://fox-toolkit.org])


dnl ------------------------|
dnl Checks for header files.|
dnl ------------------------|
AC_HEADER_STDC
dnl This is defined in config/sstream.m4 it checks for a native sstream 
dnl header and sets config.h so we'll use missing/sstream-missing if it's 
dnl not found. see config sstream.m4 for definition
ajv_CHECK_HEADER_SSTREAM


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_CHECK_FUNCS(strerror)

AC_OUTPUT(Makefile src/Makefile src/misc/Makefile src/misc/cc++/Makefile src/PoolFile/Makefile src/backend/Makefile src/backend/Edits/Makefile src/backend/Effects/Makefile src/backend/Remaster/Makefile  src/frontend_fox/Makefile) 
