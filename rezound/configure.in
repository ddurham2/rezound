dnl vim:tw=78
dnl Process this file with autoconf to build a configure script
dnl
dnl ReZound configure.in Written by Anthony Ventimiglia
dnl
dnl Copyright (C) 2002 - Anthony Ventimiglia
dnl 
dnl This file is part of ReZound, an audio editing application, 
dnl 
dnl ReZound is free software; you can redistribute it and/or modify it under the
dnl terms of the GNU General Public License as published by the Free Software
dnl Foundation; either version 2 of the License, or (at your option) any later
dnl version.
dnl 
dnl ReZound is distributed in the hope that it will be useful, but WITHOUT ANY
dnl WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
dnl FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
dnl details.
dnl 
dnl You should have received a copy of the GNU General Public License along with
dnl this program; if not, write to the Free Software Foundation, Inc., 59 Temple
dnl Place - Suite 330, Boston, MA  02111-1307, USA



dnl Process this file with autoconf to produce a configure script.
AC_INIT(config/common.h)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config/config.h)
AM_INIT_AUTOMAKE(rezound, 0.3.0alpha)
AC_DISABLE_SHARED   dnl This only builds static libs 

dnl --------------------|
dnl Checks for programs.|
dnl --------------------|
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AM_PROG_LIBTOOL
AC_PROG_CPP

dnl AC_PROG_CXX automatically sets CXXFLAGS to -g -O2 if g++ is c++ compiler
dnl and CXXFLAGS hasn't been set, so by setting CXXFLAGS, we can avoid this.
CXXFLAGS=
AC_PROG_CXX

dnl These are some custom checks to see if cc accepts some extra warning flags
dnl see config/cxx.m4 for macro definition
ajv_CXX_FLAG(-Wno-unused-function)
ajv_CXX_FLAG(-Wno-unused-variable)
ajv_CXX_FLAG(-Wno-unused)
AC_PROG_CXXCPP
AM_PROG_LEX
AC_PROG_YACC

dnl define WORDS_BIGENDIAN if this is a big endian platform
dnl NOTE: autoconf 2.5 supports passing what to do if it is a big endian 
dnl platform, but 2.13 only defines WORD_BIGENDIAN
AC_C_BIGENDIAN
AC_TRY_RUN([
	#include "confdefs.h"
	#include <stdio.h>
	int main() {
	#ifdef WORDS_BIGENDIAN
	printf("WARNING!!! ReZound has not be made to work on big endian platforms yet.  However, you are welcome to see what goes wrong.\n");
	#endif
	return 0; }
	])


dnl ---------------------|
dnl Checks for libraries.|
dnl ---------------------|
AC_CHECK_LIB(m, sqrt)


dnl  ??? on FreeBSD pthread isn't the right lib name... it should be 
dnl libc_r ("-lc_r").. so I think on the action-if-not-found parameter 
dnl to call AC_CHECK_LIB(c_r,pthread_create)  and same for the other 2
dnl I need to ask Anthony about it
AC_CHECK_LIB(pthread, pthread_create)
AC_CHECK_LIB(pthread, pthread_mutex_init)
AC_CHECK_LIB(pthread, pthread_rwlock_init)


dnl see config/cxx-lib.m4 for ajv_ macro definitions. 
dnl
dnl I found that afSetVirtualSampleFormat() was missing from earlier versions
dnl of audifile, so this is a little better than checking for a random
dnl function in libaudiofile. I still have to work this so configure will
dnl abort if the test fails.
ajv_CHECK_LIB_ABORT(audiofile, afSetVirtualSampleFormat, AUDIOFILE,[http://oss.sgi.com/projects/audiofile/])


dnl check for necessary ogg vorbis libraries and header files to be installed, 
dnl if they are not then I should print a big warning lib for fox and audiofile 
dnl saying that ogg files will not be supported and where to go get it if their 
dnl distro doesn't package it.
AC_CHECK_LIB(
	ogg,
	ogg_stream_init,

	LIBS="-logg $LIBS"
	AC_DEFINE(HAVE_LIBOGG)
	
	dnl libogg found, now check for libvorbis
	[AC_CHECK_LIB(
		vorbis, 
		vorbis_info_init,
		[

		LIBS="-lvorbis $LIBS"
		AC_DEFINE(HAVE_LIBVORBIS)

		dnl libvorbis found, now check for vorbisfile and vorbisenc header files and libraries
		AC_CHECK_HEADER(
			vorbis/vorbisfile.h,
			AC_CHECK_LIB(vorbisfile, ov_read, , AC_MSG_WARN([***** libvorbisenc.a not found; LOADING ogg vorbis support will be disabled *****]),-lvorbis),
			AC_MSG_WARN([***** vorbisfile.h header file not found -- LOADING ogg vorbis support will be disabled *****])
			)

		AC_CHECK_HEADER(
			vorbis/vorbisenc.h,
			AC_CHECK_LIB(vorbisenc, vorbis_encode_init, , AC_MSG_WARN([***** libvorbisenc.a not found; SAVING ogg vorbis support will be disabled *****]),-lvorbis),
			AC_MSG_WARN([***** vorbisenc.h header file not found -- SAVING ogg vorbis support will be disabled *****])
			)
		],

		AC_MSG_WARN([***** libvorbis.a not found -- ogg vorbis support will be disabled *****])
	)],

	AC_MSG_WARN([***** libogg.a not found -- ogg vorbis support will be disabled *****])
)


dnl This is a check for a class in a C++ library. To pinpoint
dnl the specific version we need we could change what class is linked.
ajv_CXX_CHECK_LIB([FOX],FXApp,fox/fx.h,[http://fox-toolkit.org])
dnl AC_CHECK_LIB(FOX,fxfindfox)


dnl Check for FOX's reswrap program. 
AC_PATH_PROG(RESWRAP, reswrap)
	if test -z "$RESWRAP"; then
		AC_MSG_ERROR([reswrap not found (it comes with fox)])
	fi


dnl If the user gave --enable-standalone then add -all-static and -ldl to the 
dnl linker arguments
dnl And this check has to be after the ajv_CXX_CHECK_LIB calls because it
dnl does not use libtool to compile so it fails as a valid flag to the compiler
AC_ARG_ENABLE(
	standalone,
	[  --enable-standalone     build ReZound linked only to static libraries so it can run nearly on its own],
	[
		dnl the 3rd arg would not be necessary if this section didn't have to come after ajv_CXX_CHECK_LIB's
		AC_CHECK_LIB(dl, dlopen,[LIBS="$LIBS -ldl"])
		LDFLAGS="-all-static $LDFLAGS"
		AC_DEFINE(LINKING_STATICALLY) dnl have to know this to use mypopen instead of popen because of bug in glibc
	]
	)


dnl ------------------------|
dnl Checks for header files.|
dnl ------------------------|
AC_HEADER_STDC
dnl This is defined in config/sstream.m4 it checks for a native sstream 
dnl header and makes arrangements for missing/sstream-missing to be used 
dnl if it's not found. see config sstream.m4 for definition
ajv_CHECK_HEADER_SSTREAM
dnl This is defined in config/stdint.m4 it checks for a native stdint.h 
dnl header and makes arrangements for missing/stdint.h-missing to be used 
dnl if it's not found. see config stdint.m4 for definition
ajv_CHECK_HEADER_STDINT


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
dnl These tests aren't working at all, so until we have a complaint 
dnl from someone who can't build due to a missing function, I won't worry
dnl about it
dnl AC_CHECK_FUNCS(strerror strdup strtol)

dnl AC_DEFINE_UNQUOTED Passes values through to config.h
if test x"$prefix" = x"NONE"; then
	ajv_install_prefix=/usr/local
else
	ajv_install_prefix=$prefix
fi
dnl these macros #define some cpp macros so our install paths can be 
dnl hard coded into the compiled program.
AC_DEFINE_UNQUOTED(INSTALL_PREFIX, "$ajv_install_prefix",[ This is the prefix passed to configure])

dnl and the top source directory for running pre-install
ajv_current_dir=`pwd`
ajv_source_dir=`cd $srcdir && pwd`
cd $ajv_current_dir
AC_DEFINE_UNQUOTED(SOURCE_DIR, "$ajv_source_dir",[ This is the top source directory for pre-installed testing])


if test x"$datadir" = x'${prefix}/share'; then
	ajv_data_dir=$ajv_install_prefix/`echo $datadir | sed 's%${prefix}/%%'`
else
	ajv_data_dir=$datadir
fi
AC_DEFINE_UNQUOTED(DATA_DIR, "$ajv_data_dir",[ This the --datadir (share) set by configure ])


AC_OUTPUT( \
	Makefile \
	docs/Makefile \
	share/Makefile \
	src/Makefile \
		src/misc/Makefile \
			src/misc/CNestedDataFile/Makefile \
		src/PoolFile/Makefile \
		src/backend/Makefile \
			src/backend/Edits/Makefile \
			src/backend/Effects/Makefile \
			src/backend/Filters/Makefile \
			src/backend/Looping/Makefile \
			src/backend/Remaster/Makefile \
		src/frontend_fox/Makefile \
	) 

# vim:ts=4:sw=4



# create a softlink to what should finally be the binary 
# in the frontend interface.. If there is ever a choice
# of frontend API, then this should become a conditional
# statement
rm -f ./rezound
ln -s $srcdir/src/frontend_fox/rezound ./rezound

